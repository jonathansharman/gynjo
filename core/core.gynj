{
	// List operations

	let len = l -> {
		let result = 0;
		while l != [] do {
			let result = result + 1;
			let l = pop(l);
		};
		result
	};

	let nth = (l, n) -> {
		while n > 0 do {
			let n = n - 1;
			let l = pop(l);
		};
		top(l)
	};

	let append = (l, value) ->
		if l = []
			then [value]
			else push(append(pop(l), value), top(l));

	let reverse = l -> {
		let result = [];
		while l != [] do {
			let result = push(result, top(l));
			let l = pop(l);
		};
		result
	};

	let concat = (left_list, right_list) ->
		if left_list = []
			then right_list
			else push(concat(pop(left_list), right_list), top(left_list));

	let insert = (l, index, value) ->
		if index = 0 then push(l, value) else {
			let result = [];
			let i = 0;
			while l != [] do {
				if i = index then let result = push(result, value);
				let result = push(result, top(l));
				let l = pop(l);
				let i = i + 1;
			};
			reverse(result)
		};

	let remove = (l, index) ->
		if index = 0 then pop(l) else {
			let result = [];
			let i = 0;
			while l != [] do {
				if i != index then let result = push(result, top(l));
				let l = pop(l);
				let i = i + 1;
			};
			reverse(result)
		};

	let map = (l, f) -> {
		let result = [];
		while l != [] do {
			let result = push(result, f(top(l)));
			let l = pop(l);
		};
		reverse(result)
	};

	let reduce = (l, acc, f) -> {
		while l != [] do {
			let acc = f(acc, top(l));
			let l = pop(l);
		};
		acc
	};

	let flatmap = (l, f) -> reduce(map(l, f), [], concat);

	let range = (start, end) -> {
		let result = [];
		while end >= start do {
			let result = push(result, end);
			let end = end - 1;
		};
		result
	};

	// Basic math

	let abs = x -> if x < 0 then -x else x;
	let sqrt = x -> x^(1/2);
	let cbrt = x -> x^(1/3);

	// Combinatorics

	let fact = n -> {
		let result = 1;
		while n > 1 do {
			let result = n result;
			let n = n - 1;
		};
		result
	};

	let nPk = (n, k) -> if k = 0 then 1 else n nPk(n-1, k-1);

	let nCk = (n, k) -> nPk(n, k) / fact(k);

	// Temperature conversion

	let ftoc = f -> 5/9(f - 32);
	let ctof = c -> 9/5c + 32;
}
